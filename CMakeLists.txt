##****************************************************************************
## Copyright Â© 2015 Jan Erik Breimo. All rights reserved.
## Created by Jan Erik Breimo on 2015-12-17.
##
## This file is distributed under the Simplified BSD License.
## License text is included with the source distribution.
##****************************************************************************
cmake_minimum_required(VERSION 3.16)
project(Xyz VERSION 0.1.1)

set(CMAKE_CXX_STANDARD 17)

# Set XYZ_MASTER_PROJECT to ON if Argos is the root level project.
if(NOT DEFINED XYZ_MASTER_PROJECT)
    if(CMAKE_CURRENT_SOURCE_DIR STREQUAL CMAKE_SOURCE_DIR)
        set(XYZ_MASTER_PROJECT ON)
    else()
        set(XYZ_MASTER_PROJECT OFF)
    endif()
endif()

# Test option
option(XYZ_BUILD_TEST "Build tests" ${XYZ_MASTER_PROJECT})

# Install option
option(XYZ_INSTALL "Generate the install target" ${XYZ_MASTER_PROJECT})

add_library(Xyz STATIC
    include/Xyz/Matrix.hpp
    include/Xyz/Vector.hpp
    include/Xyz/Xyz.hpp
    include/Xyz/ProjectionMatrix.hpp
    include/Xyz/MatrixTransformations.hpp
    include/Xyz/Geometry/CoordinateSystem.hpp
    include/Xyz/Geometry/Line.hpp
    include/Xyz/Geometry/LineClipping.hpp
    include/Xyz/Geometry/LineLineIntersection.hpp
    include/Xyz/Geometry/LineSegment.hpp
    include/Xyz/Geometry/Rectangle.hpp
    include/Xyz/Geometry/Triangle.hpp
    include/Xyz/Math/InvertMatrix.hpp
    include/Xyz/Math/MatrixDeterminant.hpp
    include/Xyz/Utilities/Clamp.hpp
    include/Xyz/Utilities/Comparisons.hpp
    include/Xyz/Constants.hpp
    include/Xyz/Utilities/FloatType.hpp
    include/Xyz/Utilities/RandomGenerator.hpp
    include/Xyz/Utilities/Utilities.hpp
    include/Xyz/XyzException.hpp
    src/Xyz/Utilities/RandomGenerator.cpp
    include/Xyz/LinearAlgebra.hpp)

include(GNUInstallDirs)

target_include_directories(Xyz
    PUBLIC
        $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
    PRIVATE
        ${CMAKE_CURRENT_SOURCE_DIR}/src
    )

add_library(Xyz::Xyz ALIAS Xyz)

##
## Unit tests
##

if(XYZ_BUILD_TEST)
    enable_testing()
    add_subdirectory(tests/XyzTest)
endif()

##
## "Export" the current build tree and make it possible for other modules
## in the same build tree to locate it with find_package.
##

export(TARGETS Xyz
    NAMESPACE Xyz::
    FILE XyzConfig.cmake)

##
## Installation
##

if(XYZ_INSTALL)

    install(TARGETS Xyz
        EXPORT XyzConfig
        LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
        ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
        )

    install(EXPORT XyzConfig
        FILE
            XyzConfig.cmake
        NAMESPACE
            Xyz::
        DESTINATION
            ${CMAKE_INSTALL_LIBDIR}/cmake/Xyz
        )

    install(
        DIRECTORY
            include/
        DESTINATION
            ${CMAKE_INSTALL_INCLUDEDIR}
        )

endif()
